buildscript {
	repositories {
		gradlePluginPortal()
	}
	dependencies {
		classpath "com.diffplug.spotless:spotless-plugin-gradle:6.18.0"
	}
}

repositories {
	mavenCentral()
}

apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'
apply plugin: 'com.diffplug.spotless'

group = 'com.google.gradle'
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

dependencies {
	api gradleApi()
	implementation 'com.google.code.findbugs:annotations:3.0.1'
	testImplementation 'com.google.guava:guava:20.0'
	testImplementation 'junit:junit:4.13.2'
}

gradlePlugin {
	plugins {
		thrift {
			id = 'com.google.gradle.thrift'
			implementationClass = 'co.tomlee.gradle.plugins.thrift.ThriftPlugin'
		}
	}
}

spotless {
	java {
		licenseHeaderFile 'codequality/HEADER.java'
		trimTrailingWhitespace()
	}
}

publishing {
	publications {
		maven(MavenPublication) {
			from components.java
			pom {
				name = 'gradle-thrift-plugin'
				description = "A Gradle plugin for the Thrift IDL compiler."
				licenses {
					license {
						name = 'MIT'
						url = 'https://spdx.org/licenses/MIT.html'
					}
				}
				developers {
					developer {
						id = 'thomaslee'
						name = 'Tom Lee'
						email = 'github@tomlee.co'
					}
					developer {
						id = 'shevek'
						name = 'Shevek'
						email = 'github@anarres.org'
					}
					developer {
						id = 'shevek-google'
						name = 'Shevek'
						email = 'shevek@google.com'
					}
				}
			}
		}
	}
	publications.each { publication ->
		println publication
	}
}
